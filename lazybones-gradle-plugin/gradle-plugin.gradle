apply plugin: "groovy"
apply plugin: "maven"

archivesBaseName = "lazybones-gradle"
group = "uk.co.cacoethes"
version = "1.2.2"

repositories {
    mavenCentral()
}

dependencies {
    // gradleApi() drags in the Groovy bundled with Gradle, so always a good idea
    // to specify localGroovy() too
    compile localGroovy(), gradleApi()

    testCompile "org.spockframework:spock-core:0.7-groovy-2.0", {
        exclude module: "groovy-all"
    }
    testCompile "cglib:cglib-nodep:2.2.2"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from groovydoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

uploadArchives { task ->
    repositories {
        mavenDeployer {

            // Not every build user will want to publish the plugin, so defer
            // resolution of the publishing credentials.
            gradle.taskGraph.whenReady { graph ->
                if (graph.hasTask(task)) {
                    verifyProperty(project, 'repo.url')
                    verifyProperty(project, 'repo.username')
                    verifyProperty(project, 'repo.apiKey')

                    repository url: "https://api.bintray.com/maven/pledbrook/plugins/lazybones-gradle", {
                        authentication userName: project.'repo.username', password: project.'repo.apiKey'
                    }
                }
            }
        }
    }
}

void verifyProperty(Project proj, String name) {
    if (!project.hasProperty(name)) {
        throw new GradleException("You must define the project property '$name'")
    }
}
