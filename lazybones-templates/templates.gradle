buildscript {
    repositories {
        mavenLocal()
        maven {
            url "http://dl.bintray.com/pledbrook/plugins"
        }
    }

    dependencies {
        classpath "uk.co.cacoethes:lazybones-gradle:1.2.4"
    }
}

import uk.co.cacoethes.gradle.tasks.BintrayGenericUpload

apply plugin: "uk.co.cacoethes.lazybones-templates"

lazybones {
    licenses = ["Apache-2.0"]
    publish = false
    repositoryName = "pledbrook/lazybones-templates"
    packageExclude "**/*.swp", "**/*.swo", "**/.gradle"
}

// Lazy initialisation of Bintray upload tasks so that not all build users need
// to set the repo.* project properties. The properties are only required when
// executing the publish tasks.
gradle.taskGraph.whenReady { graph ->
    if (graph.allTasks.any { it.name.startsWith("publish") }) {
        verifyProperty(project, 'repo.username')
        verifyProperty(project, 'repo.apiKey')
        verifyProperty(project, 'github.token')

        graph.allTasks.findAll { it instanceof BintrayGenericUpload }.each { task ->
            task.username = project.'repo.username'
            task.apiKey = project.'repo.apiKey'
        }

        graph.allTasks.findAll { it.name == 'publishGhPages' }.each { task ->
            task.project.githubPages.credentials.with {
                username = project.'github.token'
                //Password should be set in blank when using github token
                //https://github.com/ajoberstar/gradle-git/wiki/Travis-CI-Authentication
                password = ''
            }
        }
    }
}

void verifyProperty(Project proj, String name) {
    if (!project.hasProperty(name)) {
        throw new GradleException("You must define the project property '$name'")
    }
}
