buildscript {
    repositories {
        // Keep this for testing dev versions of the lazybones-templates plugin
//        mavenCentral()
//        mavenLocal()
        maven { url 'http://dl.bintray.com/vermeulen-mp/gradle-plugins' }
    }

    dependencies {
        // Keep this for testing dev versions of the lazybones-templates plugin
//        classpath "uk.co.cacoethes:lazybones-gradle:1.2.3"
        classpath 'net.gvmtool:gradle-sdkvendor-plugin:0.4.0'
    }
}

import uk.co.cacoethes.gradle.tasks.BintrayGenericUpload

plugins {
    id "net.saliman.cobertura" version "2.2.7"
    id "uk.co.cacoethes.lazybones-templates" version "1.2.3"
}

apply plugin: "groovy"
apply plugin: "application"
apply plugin: "codenarc"
apply plugin: "net.gvmtool.sdkvendor"

applicationName = "lazybones"

// These settings mimic the old client VM behavior. Should result in faster startup.
mainClassName = "uk.co.cacoethes.lazybones.cli.LazybonesMain"
version = "0.8.3"
applicationDefaultJvmArgs = ["-XX:+TieredCompilation", "-XX:TieredStopAtLevel=1", "-XX:CICompilerCount=3"]

sourceCompatibility = "1.6"
targetCompatibility = "1.6"

ext {
    groovyVersion = "2.4.3"
    cachePath = new File(System.getProperty("user.home"), ".lazybones/templates").absolutePath
    isCiBuild = System.getProperty("drone.io") as boolean
    testWorkDir = file("$buildDir/testWork").path
}

sourceSets {
    integTest {
        groovy {
            srcDir "src/integ/groovy"
            compileClasspath += sourceSets.main.output
        }
        resources {
            srcDir "src/integ/resources"
        }
    }
}

configurations {
    integTestCompile.resolutionStrategy {
        force "org.codehaus.groovy:groovy-all:${groovyVersion}"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.codehaus.groovy:groovy:${groovyVersion}"

    compile "commons-io:commons-io:2.4",
            "org.codehaus.groovy:groovy-json:${groovyVersion}",
            "org.codehaus.groovy:groovy-templates:${groovyVersion}",
            "org.apache.commons:commons-compress:1.5",
            "com.github.groovy-wslite:groovy-wslite:1.1.2",
            "net.sf.jopt-simple:jopt-simple:4.4",
            "org.ini4j:ini4j:0.5.2",
            "com.squareup.dagger:dagger:1.2.2",
            "com.squareup.dagger:dagger-compiler:1.2.2"

    runtime "org.apache.ivy:ivy:2.3.0"

    testCompile "org.spockframework:spock-core:0.7-groovy-2.0", {
        exclude module: "groovy-all"
    }

    integTestCompile "org.codehaus.groovy:groovy-all:2.4.3",
                     "commons-io:commons-io:2.4",
                     "co.freeside:betamax:1.1.2",
                     "org.littleshoot:littleproxy:1.1.0-beta1"

    integTestCompile "org.spockframework:spock-core:1.0-groovy-2.4"
}

idea {
    module {
        scopes.PROVIDED.plus += [configurations.integTestCompile]
    }
}

jar {
    manifest {
        attributes("Implementation-Title": "Lazybones", "Implementation-Version": version)
    }
}

gvm {
    api = "https://gvm-vendor.herokuapp.com/"
    candidate = project.applicationName
    version = project.version
    url = "https://bintray.com/artifact/download/pledbrook/lazybones-templates/${project.applicationName}-${project.version}.zip"
    hashtag = "#lazybones"
}

lazybones {
    templateDirs = files(file("${projectDir}/src/integTest/templates").listFiles())
    template "subtemplates-tmpl" includes "controller", "entity", "bad"
    template "test-handlebars", {
        version = "0.1.1"
    }
}

compileGroovy {
    groovyOptions.javaAnnotationProcessing = true
}

packageAllTemplates.dependsOn("packageTemplate-Oops-stuff")
installAllTemplates.dependsOn("installTemplate-Oops-stuff")

task configureCachePath {
    if (isCiBuild) {
        // Load the default cache directory location from the default config file.
        // We have to defer the evaluation until after the test resources have been
        // copied onto the classpath.
        def defaultConfigFile = sourceSets.main.resources.filter { it.name == "uk.co.cacoethes.lazybones.config.defaultConfig.groovy" }.singleFile
        def defaultConfig = new ConfigSlurper().parse(defaultConfigFile.text)
        project.cachePath = defaultConfig.cache.dir
    }
    else {
        project.cachePath = testWorkDir + "/template-cache"
    }

    project.extensions.lazybones.installDir = new File(project.cachePath)
}

//for a single test, you can run "gradle -DintegTest.single=<test name>"
task integTest(type: Test) {
    dependsOn "installDist", "configureCachePath", "installAllTemplates"
    inputs.dir sourceSets.main.output.classesDir
    inputs.dir sourceSets.main.output.resourcesDir

    mustRunAfter "test"
    ignoreFailures = true

    systemProperty "integration.test", "true"
    systemProperty "lazybones.testWorkDir", testWorkDir
    systemProperty "lazybones.installDir", installDist.destinationDir.path
    systemProperty "lzbtest.expected.version", version

    // Allows us to disable tests that don't work on Drone.io, such as the
    // CreateFunctionalSpec feature test for --with-git.
    systemProperty "lazybones.config.file",
                   new File(sourceSets.integTest.output.resourcesDir, "test-config.groovy").absolutePath

    def cachePath
    if (isCiBuild) {
        systemProperty "drone.io", "true"
    }
    else {
        // Use the default cache location on the CI server, but a custom one
        // on local machines to avoid polluting the developer's own cache.
        systemProperty "lazybones.cache.dir", project.cachePath
    }

    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath

    include "**/*Spec*"
    exclude "**/Abstract*Spec*"
}

task packageReports(type: Zip) {
    from "build/reports"
    archiveName = "reports.zip"
    destinationDir = buildDir
}

integTest.finalizedBy packageReports

codenarc {
    configFile = rootProject.file("codenarc.groovy")
}
codenarcMain.excludes = ["**/NameType.groovy"]
codenarcTest.enabled = false
codenarcIntegTest.enabled = false

/** Cobertura (Coverage) Configuration */
cobertura {
    coverageFormats = ['html', 'xml']
    coverageSourceDirs = sourceSets.main.allSource.srcDirs
}

distZip.dependsOn("test", "integTest")
check.dependsOn("test", "integTest")

task uploadDist(type: BintrayGenericUpload, dependsOn: "distZip") {
    artifactFile = distZip.archivePath
    artifactUrlPath = "lazybones/${version}/${distZip.archiveName}"
    repositoryName = "pledbrook/lazybones-templates"
    packageName = "lazybones"
    licenses = ["Apache-2.0"]
}

task release(dependsOn: ["uploadDist", "sdkMajorRelease"])
sdkMajorRelease.mustRunAfter uploadDist

// Lazy initialisation of uploadDist task so that not all build users need to
// set the repo.* project properties.
//
// We also lazily configure the cache directory system property when running
// the integration tests. This is to ensure that the test-tmpl template gets
// installed into the cache directory being used by the integration tests.
gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(":lazybones-app:uploadDist")) {
        verifyProperty(project, 'repo.username')
        verifyProperty(project, 'repo.apiKey')

        uploadDist.username = project.'repo.username'
        uploadDist.apiKey = project.'repo.apiKey'
    }

    if (graph.hasTask(":lazybones-app:sdkMajorRelease")) {
        verifyProperty(project, 'gvm.consumerKey')
        verifyProperty(project, 'gvm.consumerToken')
        sdkMajorRelease.consumerKey = project.'gvm.consumerKey'
        sdkMajorRelease.consumerToken = project.'gvm.consumerToken'
    }
}

void verifyProperty(Project proj, String name) {
    if (!project.hasProperty(name)) {
        throw new GradleException("You must define the project property '$name'")
    }
}
